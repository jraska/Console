apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
apply plugin: 'signing'

version = rootProject.ext.version
group = 'com.jraska'

android {
  compileSdkVersion rootProject.ext.compileSdkVersion

  defaultConfig {
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion
    versionName version
    versionCode rootProject.ext.versionCode
  }

  compileOptions {
    sourceCompatibility rootProject.ext.sourceCompatibility
    targetCompatibility rootProject.ext.sourceCompatibility
  }

  lintOptions {
    abortOnError true
  }

  testOptions {
    unitTests.includeAndroidResources true
  }
}

dependencies {
  api 'com.jakewharton.timber:timber:4.7.1'

  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

  implementation project(":console")

  testImplementation 'junit:junit:4.13.2'
  testImplementation 'org.mockito:mockito-core:3.10.0'
  testImplementation 'org.assertj:assertj-core:3.19.0'
  testImplementation 'org.robolectric:robolectric:4.4'
}

task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  archiveClassifier = 'sources'
}

artifacts {
  archives sourcesJar
}

afterEvaluate {
  publishing {
    publications {
      release(MavenPublication) {
        groupId 'com.jraska'
        artifactId 'console-timber-tree'
        version rootProject.ext.version

        from components.release
        artifact sourcesJar

        pom {
          name = 'com.jraska'
          description = 'Timber tree for com.jraska:console'
          url = 'https://github.com/jraska/Console'
          licenses {
            license {
              name = 'The Apache Software License, Version 2.0'
              url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }
          developers {
            developer {
              id = 'jraska'
              name = 'Josef Raska'
            }
          }

          scm {
            connection = 'scm:git:github.com/jraska/Console.git'
            developerConnection = 'scm:git:ssh://github.com/jraska/Console.git'
            url = 'https://github.com/jraska/Console/tree/master'
          }
        }
      }
    }
  }
}

// Adding these directly into signing {} below didn't work :idk
ext["signing.keyId"] = System.getenv("SIGNING_KEY_ID")
ext["signing.password"] = System.getenv("SIGNING_KEY_PASSWORD")
ext["signing.secretKeyRingFile"] = "$buildDir/signing-key.gpg"

signing {
  sign publishing.publications
}
